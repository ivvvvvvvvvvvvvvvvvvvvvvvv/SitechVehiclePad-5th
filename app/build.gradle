import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'android-aspectjx'
apply plugin: 'com.alibaba.arouter'

String getCurrentTime(String formatString) {
    SimpleDateFormat df = new SimpleDateFormat(formatString)
    df.format(new Date())
}

android {
    compileSdkVersion Config.compileSdkVersion
    defaultConfig {
        applicationId Config.applicationId
        minSdkVersion Config.minSdkVersion
        targetSdkVersion Config.targetSdkVersion
        versionCode Config.appVersionCode
        versionName Config.appVersionName
        multiDexEnabled true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [eventBusIndex      : "com.sitechdev.vehicle.pad.utils.MyEventBusIndex",
                             AROUTER_MODULE_NAME: project.getName()]
            }
        }
        ndk {
            // 设置支持的SO库架构
            abiFilters 'armeabi-v7a'//,'armeabi',  'x86'//, 'x86_64', 'arm64-v8a'
        }
        resConfigs "zh"
        buildConfigField("String", "AIUI_APPID", "\"5e97d73d\"")
    }
    signingConfigs {
        release {
            storeFile file(Config.signInfo.storeKey)
            storePassword Config.signInfo.storePassword
            keyAlias Config.signInfo.keyAlias
            keyPassword Config.signInfo.keyPassword
        }
    }
    repositories {
        flatDir {
            dirs 'libs/'
        }
    }

    aspectjx {
        //排除所有package路径中包含`android.support`的class文件及库（jar文件）
        exclude 'android.support', 'androidx', 'com.google', 'com.squareup', 'com.alipay', 'org.apache'
    }
    /*
    * 渠道Flavors配置
    * */
    flavorDimensions "product"
    productFlavors {
        preview {
            //Preview 演示预览版本
            buildConfigField("Boolean", "isPreview", "true")
        }
        product {
            //product 正式量产版本
            buildConfigField("Boolean", "isPreview", "false")
        }
    }
    //配置资源文件路径
    sourceSets {
        preview {
            //Preview 版本
            java.srcDirs = ['src/preview/java']
            res.srcDirs = ['src/preview/res', 'src/preview/res-whiteorange', 'src/preview/res-blueorange']
            assets.srcDirs = ['src/main/assets', 'src/main/assets/aiui_5e97d73d/']
            aidl.srcDirs = ['src/main/aidl']
            jniLibs.srcDirs = ['src/main/jniLibs', 'libs/']
        }
        product {
            //product版本
            java.srcDirs = ['src/product/java']
            res.srcDirs = ['src/product/res', 'src/product/res-whiteorange', 'src/product/res-blueorange']
            assets.srcDirs = ['src/main/assets', 'src/main/assets/aiui_5e97d73d/']
            aidl.srcDirs = ['src/main/aidl']
            jniLibs.srcDirs = ['src/main/jniLibs', 'libs/']
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.release
            debuggable true
            jniDebuggable true
            zipAlignEnabled true
            versionNameSuffix "." + getCurrentTime("YYMMddHHmm").toString() + ".debug"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            debuggable false
            jniDebuggable false
            zipAlignEnabled true
            versionNameSuffix "." + getCurrentTime("YYMMddHHmm").toString()
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility Config.sourceCompatibilityVersion
        targetCompatibility Config.targetCompatibilityVersion
    }
    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
    packagingOptions {
        exclude 'META-INF/rxjava.properties'
        pickFirst 'lib/armeabi-v7a/libc++_shared.so'
        pickFirst 'lib/x86/libc++_shared.so'
        pickFirst 'lib/arm64-v8a/libc++_shared.so'
        pickFirst 'lib/armeabi/libc++_shared.so'
    }

    repositories {
        flatDir {
            dirs 'libs'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation Config.depConfig.android_support.appcompatV7
    implementation Config.depConfig.android_support.multidex
    implementation Config.depConfig.android_support.recyclerview
    implementation Config.depConfig.android_support.constraint

    annotationProcessor Config.depConfig.eventbus.eventbusProcessor
    implementation(Config.depConfig.tkrefreshlayout) {
        exclude module: 'recyclerview-v7'
        exclude module: 'support-v4'
    }

    annotationProcessor Config.depConfig.ali.arouter_compiler

    //============高德地图sdk================
    //3D地图so及jar
    implementation Config.depConfig.amap.amap
    //定位功能
    implementation Config.depConfig.amap.amapLocation
    //搜索功能
    implementation Config.depConfig.amap.amapSearch
    implementation Config.depConfig.android_support.design
    //============高德地图================

    //网络组件。
    implementation project(':netLibrary')
    //全局工具组件
    implementation project(':utilLibrary')

    //============图片选择组件================
    implementation project(':picture_library')
    implementation project(':ucrop')
    implementation('jp.wasabeef:glide-transformations:2.0.2') {
        exclude module: 'glide'
    }
    implementation 'jp.co.cyberagent.android.gpuimage:gpuimage-library:1.4.1'
    //============图片选择组件================

    implementation Config.depConfig.dataStatistics

    //============听伴sdk================
//    implementation(Config.depConfig.tingban) {
//        changing = true
//        exclude module: 'gson'
//    }
    //引入考拉jar包的依赖包，临时方案
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'
    implementation 'org.greenrobot:greendao:3.2.2'
    implementation 'com.google.dagger:dagger:2.16'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.16'
    implementation 'com.android.volley:volley:1.1.0'
    //============听伴sdk================
    implementation Config.depConfig.tinypinyin

    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.20'

    implementation Config.depConfig.FlycoTabLayout
    //换肤控件
    implementation Config.depConfig.skin_support.skin_support
    implementation Config.depConfig.skin_support.skin_support_design
    implementation Config.depConfig.skin_support.skin_support_constraint_layout
    //
    implementation Config.depConfig.rollingText
    //通用组件
    implementation project(':library:CommonLibrary')
    implementation project(':library:ToucheffectsLib')

    //下拉刷新
    implementation Config.depConfig.smartrefresh
    implementation 'com.gcssloop.widget:rclayout:1.8.1'
}

android.applicationVariants.all { variant ->
    variant.mergeAssets.doLast {
        //删除assets文件夹下的所有aiui_5e97d73d目录文件
        delete(fileTree(dir: variant.mergeAssets.outputDir, includes: ['aiui_5e97d73d/**']))
    }
    def fileName = PACKAGE_NAME_PRE
    def apkProvider = variant.getPackageApplicationProvider().get().outputScope.apkDatas
    apkProvider.forEach { apkData ->
        def isRelease = "release".equalsIgnoreCase(variant.buildType.name)
        apkData.outputFileName = fileName + (isRelease ? "_Release_" : "_Debug_") + "${variant.productFlavors[0].name}_v" + versionName + ".apk"
    }
}
